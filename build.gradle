plugins {
    id 'java'
    id 'maven'
}

group 'de.wolfig'
version '1.0'

task createJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'lexisnexis_heideltime',
                'Implementation-Version': version,
                'Main-Class': 'de.wolfig.Main'
    }
    def getGitHash = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    baseName = project.name + '-' + getGitHash()
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    with jar
}

task writeNewPom {
    doLast {
        pom {
            project {
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
            }
        }.writeTo("./pom.xml")
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.konghq', name: 'unirest-java', version: '3.7.02'
    compile group: 'javax.ws.rs', name: 'jsr311-api', version: '1.1.1'
    compile group: 'com.sun.jersey', name: 'jersey-bundle', version: '1.19.4'
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-xml', version: '2.11.0'
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-csv', version: '2.11.0'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.2'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.2'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

